/*
 * Copyright (c) 2014 mgm technology partners GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.mgmtp.perfload.supervisor.Commands
import com.mgmtp.perfload.supervisor.SupervisorTasks
import com.mgmtp.perfload.supervisor.SupervisorUtils
import org.apache.commons.io.FilenameUtils
import org.apache.commons.lang3.StringUtils
import org.apache.commons.lang3.time.StopWatch

import java.text.DateFormat
import java.text.SimpleDateFormat
import java.util.Collections

buildscript {
	dependencies {
		classpath fileTree(dir: 'lib')
	}
}

defaultTasks 'runLoadTest'

def config() {
	project.ext {
		if (!project.hasProperty('tenant')) {
			tenant = System.getenv('PERFLOAD_TENANT')
		}

		supervisorConfig = SupervisorUtils.loadConfig(tenant, 'SupervisorConfig.groovy')
		commands = SupervisorUtils.loadConfig(Commands.class)
		loadTestConfig = SupervisorUtils.loadTestConfig(tenant, 'LoadTestConfig.groovy')
		supervisorTasks = new SupervisorTasks(ant: ant, loadTestConfig: loadTestConfig, commands: commands)

		resultsDir = "$projectDir/results"
		consoleDir = "$projectDir/../console"
		perfAlyzerDir = "$projectDir/../perfalyzer"

		if (!supervisorConfig.requireTestComment) {
			testComment = ''
		}
	}
	if (supervisorConfig.runProjectSpecificTasks) {
		// includes project-specific targets as if they were directly defined in this file
		String projectTasksFile = "conf/${tenant ? tenant + '/' : ''}ProjectTasks.gradle"
		println "Including project targets file: $projectTasksFile"

		apply from: projectTasksFile
	}
}

def welcome() {
	String msgTemplate = getClass().getResource('/supervisor-msg.txt').getText('UTF-8')
	int len = 60

	println msgTemplate
			.replace('@msg1@', '(c) 2013 - 2016, mgm technology partners GmbH'.center(len))
			.replace('@msg2@', 'Welcome to perfLoad\'s Supervisor ${project.version}'.center(len))

	println()

	if (tenant) {
		println "Running in multi-tenancy mode. Tenant: $tenant."
	} else {
		println 'No tenant specified. Running in single-tenancy mode.'
	}

	println()
	println()
}

task init(description: 'Initializes the Supervisor loading basic configuration for testplan-independent tasks') {
	config()
	welcome()
}

task loadTestplan(description: 'Loads the test plan possibly prompting for the testplan to be used.') << {
	// if not set via command-line (-Ptestplan=<...>), the user is prompted for the test config to be used
	if (!project.hasProperty('testplan')) {
		project.ext.testplan = promptForTestplan()
	}

	// Use the testplan file's base name as the test name and add it to the script's binding.
	// Thus, it is also accessible by an included 'ProjectTargets.gradle' file.
	project.ext.testname = FilenameUtils.getBaseName(testplan)

	// prefix the results directory with a timestamp
	DateFormat df = new SimpleDateFormat('yyyyMMdd-HHmm')

	def dateString = project.hasProperty('testDate') ? testDate : df.format(new Date())
	def testResultsDir = new File(resultsDir, "${tenant ? tenant + '/' : ''}${dateString}_${FilenameUtils.getBaseName(testplan)}")

	project.ext.testResultsDir = testResultsDir
	// sys prop necessary for logging to file, used in init.gradle
	System.setProperty('testResultsDir', testResultsDir.path)

	testResultsDir.mkdirs()
	supervisorTasks.resultsDir = testResultsDir
}

task runLoadTest(dependsOn: loadTestplan, description: 'Runs a load test.') << {
	// if not set via command-line (-DtestComment=<...>), the user is prompted for the test config to be used
	if (!project.hasProperty('testComment')) {
		project.ext.testComment = new Console().readLine('\nPlease enter some comment for the test:')
	}

	def tasks = []

	if (supervisorConfig.cleanupBeforeTest) {
		tasks << cleanupFiles
	}

	if (supervisorConfig.runProjectSpecificTasks) {
		tasks << before
	}

	tasks << startPerfmons
	tasks << restartDaemons

	if (supervisorConfig.executeStartupCommands) {
		tasks << executeStartupCommands
	}

	if (supervisorConfig.runProjectSpecificTasks) {
		tasks << performSystemCheck
	}

	tasks << startTest

	if (supervisorConfig.executeShutdownCommands) {
		tasks << executeShutdownCommands
	}

	tasks << stopDaemons
	tasks << stopPerfmons

	tasks << createTransferDirs

	if (supervisorConfig.runProjectSpecificTasks) {
		tasks << after
	}

	task writeTestComment << {
		new File(consoleDir, 'perfload.meta.utf8.props').withWriterAppend('UTF-8') { writer ->
			new PrintWriter(writer).println("test.comment=${testComment}")
		}
	}
	tasks << writeTestComment

	if (supervisorConfig.collectResults) {
		tasks << archiveFiles
		tasks << downloadFiles
	}

	if (supervisorConfig.createReport) {
		tasks << runPerfAlyzer
	}

	StopWatch stopWatch = new StopWatch()
	stopWatch.start()

	tasks.each { it.execute() }

	stopWatch.stop()

	println()
	println 'LOAD TEST EXECUTION FINISHED'
	println()
	println "Total execution time: $stopWatch"
}

task startTest(dependsOn: loadTestplan, description: 'Starts a load test') << {
	List daemons = SupervisorUtils.readDaemonsFromConfig(loadTestConfig)
	SupervisorUtils.executeCommandLine("./console", consoleDir,
			[
					'-testplan',
					testplan,
					'-daemons',
					daemons.join(','),
					'-timeout',
					"${supervisorConfig.loadProfileTestTimeout}"
			].flatten())
}

task abortLoadTest(dependsOn: loadTestplan, description: 'Aborts a load test') << {
	List daemons = SupervisorUtils.readDaemonsFromConfig(loadTestConfig)
	SupervisorUtils.executeCommandLine("./console", consoleDir,
			[
					'-testplan',
					testplan,
					'-daemons',
					daemons.join(','),
					'-abort'
			].flatten())
}

task runPerfAlyzer(dependsOn: loadTestplan, description: 'Run perfAlyzer to create the report') << {
	String absoluteResultsDir = testResultsDir.getAbsolutePath()

	SupervisorUtils.executeCommandLine("./perfalyzer", perfAlyzerDir,
			[
					'-i',
					absoluteResultsDir,
					'-o',
					"output${tenant ? '/' + tenant : ''}"
			])
}

task startDaemons(description: 'Starts daemons') << { supervisorTasks.startDaemons() }

task stopDaemons(description: 'Stops daemons') << { supervisorTasks.stopDaemons() }

task restartDaemons(description: 'Restart daemons') << {
	supervisorTasks.stopDaemons()
	sleep 5000L
	supervisorTasks.startDaemons()
}

task startPerfmons(description: 'Starts perfmons') << { supervisorTasks.startPerfmons() }

task stopPerfmons(description: 'Stops perfmons') << { supervisorTasks.stopPerfmons() }

task cleanupFiles(description: 'Perform all clean-up tasks.') << {
	cleanupConsoleFiles()
	supervisorTasks.cleanupDaemonFiles()
	supervisorTasks.cleanupClientFiles()
	supervisorTasks.cleanupPerfmonFiles()
	supervisorTasks.cleanupConfiguredFiles()
}

task createTransferDirs(description: 'Creates transfer directories') << { supervisorTasks.createTransferDirs() }

task executeStartupCommands(description: 'Executes configured startup commands') << { supervisorTasks.execStartupCommands() }

task executeShutdownCommands(description: 'Executes configured shutdown commands') << { supervisorTasks.execShutdownCommands() }

task archiveFiles(dependsOn: loadTestplan, description: 'Archives all result files via SSH') << {
	archiveConsoleFiles()
	supervisorTasks.archiveDaemonLogs()
	supervisorTasks.archiveClientLogs()
	supervisorTasks.archivePerfmonLogs()
	supervisorTasks.archiveConfiguredFiles()
}

task downloadFiles(dependsOn: loadTestplan, description: 'Downloads all result files via SCP.') << {
	supervisorTasks.downloadDaemonLogs()
	supervisorTasks.downloadClientLogs()
	supervisorTasks.downloadPerfmonLogs()
	supervisorTasks.downloadConfiguredFiles()
}

task listTestplans(description: 'Display a list of available testplans') << {
	List<String> testplans = readTestplans(tenant)
	println listTestplans(testplans)
}

def String promptForTestplan() {
	List<String> testplans = readTestplans(tenant)
	
	String lineSep = System.properties.'line.separator'
	String prompt = "${lineSep}Please select the testplan you'd like to use:$lineSep"
	prompt += listTestplans(testplans)

	// display all configured testplans prefixed by running numbers,
	// which the user needs to enter in order to select the desired testplan
	while (true) {
		def testplanIndex = new Console().readLine(prompt)
		int index

		try {
			index = Integer.parseInt(testplanIndex) - 1
			if (index < 0 || index >= testplans.size()) {
				continue
			}
		} catch (NumberFormatException ex) {
			continue
		}

		def selectedTestplan = testplans[index]

		println()
		println "Selected testplan: '$selectedTestplan'..."
		println()

		return "${tenant ? tenant + '/' : ''}$selectedTestplan"
	}
}

def List<String> readTestplans(String tenant) {
	List<String> testplans = []
	new File(consoleDir, "testplans/${tenant ? tenant : ''}").eachFileMatch(~/(?!testjars).*\.xml/, { testplans << it.name })
	Collections.sort(testplans)
	testplans
}

def String listTestplans(List<String> testplans) {
	String lineSep = System.properties.'line.separator'
	int i = 1
	testplans.collect { "${StringUtils.leftPad(String.valueOf(i++), 3)}) $it" }.join(lineSep) + lineSep
}

def archiveConsoleFiles() {
	File testplanFile = new File("${consoleDir}/testplans/${testplan}")
	def config = new XmlSlurper().parse(testplanFile)
	String eventsFile = config.loadProfile

	task(archiveConsoleFiles, type: Zip) {
		from(consoleDir) {
			include '*.log'
			include 'ltStatus.txt'
			include 'ltThreads.txt'
			include 'perfload.meta.utf8.props'
		}
		from(testplanFile.parent) { include testplanFile.name }
		from("${testplanFile.parent}/loadprofiles") { include eventsFile }
		archiveName = "console-logs.zip"
		destinationDir = file("${testResultsDir}/console")
	}.execute()
}

def cleanupConsoleFiles() {
	ant.delete {
		fileset(dir: "${consoleDir}") {
			include(name: '*.log')
			include(name: 'ltStatus.txt')
			include(name: 'ltThreads.txt')
			include(name: 'perfload.meta.utf8.props')
		}
	}
}

class Console {

	private final java.io.Console console
	private BufferedReader oldFashionedReader

	Console() {
		console = System.console()
		if (console == null) {
			oldFashionedReader = new BufferedReader(new InputStreamReader(System.in))
		}
	}

	String readLine(String prompt) {
		if (console != null) {
			return console.readLine(prompt)
		} else {
			try {
				println(prompt)
				return oldFashionedReader.readLine()
			} catch (IOException ex) {
				// can't really happen
				throw new UncheckedIOException(ex)
			}
		}
	}
}
